#!/usr/bin/env perl
# gemapass : Generate and Mail Passwords
#
# Copyright (c) 2012 Etienne Millon <etienne.millon@gmail.com>
# ----------------------------------------------------------------------------
#                        "THE BEER-WARE LICENSE"
# <etienne.millon@gmail.com> wrote this file. As long as you retain this notice
# you can do whatever you want with this stuff. If we meet some day, and you
# think this stuff is worth it, you can buy me a beer in return.
# ----------------------------------------------------------------------------
use strict;
use warnings;
use Digest::MD5 qw/md5_hex/;
use Getopt::Euclid;
use MIME::Lite;
use Template;
use YAML::Syck;

&main();
exit;

sub generate_password {
  my $length = shift;
  my $possible = 'abcdefghituvwxyz23456789ABCDEFGHJKLMNPQRSTUVWXYZ';
  my $password = '';
  while (length($password) < $length) {
    $password .= substr($possible, (int(rand(length($possible)))), 1);
  }
  return $password
}

sub build_mail {
    my $mailinfo = shift;
    my $body = shift;
    my $msg = MIME::Lite->new(%$mailinfo, Data => $body);

    return $msg->as_string;
}

sub htdigest_line {
  my ($username, $realm, $password) = @_;
  my $hashed_pw = md5_hex("$username:$realm:$password");
  return "$username:$realm:$hashed_pw\n";
}

sub build_body {
  my ($template, $params) = @_;

  my $data;
  my $tt = Template->new({ABSOLUTE => 1, RELATIVE => 1})
  || die Template->error(), "\n";
  $tt->process($template, $params, \$data);
  return $data;
}

sub output_htpasswd_line {
  my $line = shift;
  if ($ARGV{'-E'}) {
    print $line;
  } else {
    print HTPASSWD $line;
  }
}

sub output_mail {
  my ($mail_filename, $msg) = @_;
  if ($ARGV{'-E'}) {
    print "---\n";
    print $msg;
  } else {
    open MAIL, '>', $mail_filename
      or die "Cannot open $mail_filename : $!";
    print MAIL $msg;
    close MAIL
      or die "Cannot close $mail_filename";
  }
}

sub write_mail {
  my ($conf, $mail_dirname, $username, $user) = @_;

  my $template = $conf->{'template'};

  my $realm = $conf->{'realm'};

  my $password = &generate_password(10);
  my %params = ( username => $username,
                 fullname => $user->{'fullname'},
                 password => $password,
                 realm    => $realm,
               );
  my $body = &build_body(\$template, \%params);
  my $msg = &build_mail({ From => $conf->{'from'},
                          To => $user->{'email'},
                          Subject => $conf->{'subject'},
                          Bcc => $conf->{'bcc'},
                        }, $body);

  my $hline = &htdigest_line($username, $realm, $password);
  &output_htpasswd_line ($hline);

  my $mail_filename = "$mail_dirname/$username.mail";

  &output_mail ($mail_filename, $msg);
}

sub main {
  my $conf = LoadFile($ARGV{'-c'});

  my $htpasswd_filename = 'htpasswd.out';
  unless ($ARGV{'-E'}) {
    open HTPASSWD, '>>', $htpasswd_filename
      or die "Cannot open $htpasswd_filename";
  }

  my $mail_dirname = 'mail.out';

  unless ($ARGV{'-E'}) {
    mkdir $mail_dirname
      or die "Cannot mkdir $mail_dirname";
  }

  my @usernames;
  my $ulist = $ARGV{'-u'};
  if (defined $ulist) {
    @usernames = split (',', $ulist);
  } else {
    @usernames = keys $conf->{'users'};
  }

  for my $username (@usernames) {
    my $user = $conf->{'users'}{$username};
    &write_mail($conf, $mail_dirname, $username, $user);
  }

  unless ($ARGV{'-E'}) {
    close HTPASSWD
      or die "Cannot close $htpasswd_filename";
  }
}

__END__

=head1 NAME

gemapass - Generate & Mail Passwords

=head1 USAGE

    gemapass -c conf.yml [-u users]

=head1 REQUIRED ARGUMENTS

=over

=item -c <conffile>

Where to read password configuration.

=back

=head1 OPTIONS

=over

=item -u <userlist>

When this option is present, only users in "userlist" (comma-separated) will be
handled.

=item -E

Output htpasswd and mails to stdout.

=back

=head1 CONFIGURATION FILE SYNTAX

The configuration file is in YAML format. Example :

    from: admin@example.net
    realm: example.net
    subject: Your account on example.net
    bcc: bcc@example.net
    template: |
        Hello [% fullname %], here are your credentials on [% realm %] :
    
        Username : [% username %]
        Password : [% password %]
    users:
      michel:
        fullname: 'Michel Demichel'
        email: 'michel.demichel@example.net'
      joe:
        fullname: 'Joe Lagachette'
        email: 'joe.lagachette@example.net'
