#!/usr/bin/env perl
# gemapass : Generate and Mail Passwords
#
# Copyright (c) 2012 Etienne Millon <etienne.millon@gmail.com>
# ----------------------------------------------------------------------------
#                        "THE BEER-WARE LICENSE"
# <etienne.millon@gmail.com> wrote this file. As long as you retain this notice
# you can do whatever you want with this stuff. If we meet some day, and you
# think this stuff is worth it, you can buy me a beer in return.
# ----------------------------------------------------------------------------
use strict;
use warnings;
use Digest::MD5 qw/md5_hex/;
use Getopt::Euclid;
use MIME::Lite;
use Template;

my $template = <<TEMPLATE;
Username : [% name %]
Password : [% password %]
TEMPLATE

my $username = 'Michel';
my $realm = 'example.net';

my $password = &generate_password(10);
my %params = (name => $username, password => $password);
my $body = &build_body(\$template, \%params);

print &htdigest_line($username, $realm, $password);
print "---\n";
print &build_mail ({ From => 'me@example.net',
                     To => 'you@example.net',
                     Subject => 'Your password',
                   }, $body);

exit;

sub generate_password {
  my $length = shift;
  my $possible = 'abcdefghituvwxyz23456789ABCDEFGHJKLMNPQRSTUVWXYZ';
  my $password = '';
  while (length($password) < $length) {
    $password .= substr($possible, (int(rand(length($possible)))), 1);
  }
  return $password
}

sub build_mail {
    my $mailinfo = shift;
    my $body = shift;
    my $msg = MIME::Lite->new(%$mailinfo, Data => $body);

    return $msg->as_string;
}

sub htdigest_line {
  my $username = shift;
  my $realm = shift;
  my $password = shift;
  my $hashed_pw = md5_hex("$username:$realm:$password");
  return "$username:$realm:$hashed_pw\n";
}

sub build_body {
  my $template = shift;
  my $params = shift;

  my $data;
  my $tt = Template->new({ABSOLUTE => 1, RELATIVE => 1})
  || die Template->error(), "\n";
  $tt->process($template, $params, \$data);
  return $data;
}

__END__

=head1 NAME

gemapass - Generate & Mail Passwords
